-* WebFOCUS test source file for pygments.

-* Some basic commands
-TYPE hello world!
-SET &NAME='John';
-TYPE hello &NAME !
-SET &DATETMP = DATECVT((DATEADD((DATECVT(&DATEIN, 'I8YYMD', 'YYMD')),'M', -6)),'YYMD','I8YYMD');

-GOTO LABEL1;
-TYPE Skip me

-* Label using classic syntax.
-LABEL1

-* Label using modern syntax.
-:LABEL2

-* Use FIDEL with MODIFY.
MODIFY FILE EMPLOYEE
CRTFORM
  "EMPLOYEE UPDATE"
  "EMPLOYEE ID #: <EMP_ID   LAST NAME: <LAST_NAME"
  "DEPARTMENT: <DEPARTMENT  SALARY: <CURR_SAL"
MATCH EMP_ID
  ON NOMATCH REJECT
  ON MATCH UPDATE LAST_NAME DEPARTMENT CURR_SAL
DATA
END

-* Use FIDEL with dialog manager.
-CRTFORM
-"MONTHLY SALES REPORT FOR <&CITY/10"
-"BEGINNING PRODUCT CODE IS: <&CODE1/3"
-"ENDING PRODUCT CODE IS: <&CODE2/3"
-"REGIONAL SUPERVISOR IS: <&REGIONMGR/5"
TABLE FILE SALES
  HEADING CENTER
    "MONTHLY REPORT FOR &CITY"
    "PRODUCT CODES FROM &CODE1 TO &CODE2"
    ""
  SUM UNIT_SOLD AND RETURNS AND COMPUTE
  RATIO/D5.2 = 100 * RETURNS/UNIT_SOLD;
  BY PROD_CODE
  IF PROD_CODE IS-FROM &CODE1 TO &CODE2
  FOOTING CENTER
    "REGIONAL SUPERVISOR: &REGIONMGR"
END

-* Aggregate and rank sort field values.
TABLE FILE MOVIES
  SUM WHOLESALEPR CNT.WHOLESALEPR
  BY CATEGORY
  RANKED BY HIGHEST 2 TOTAL AVE.WHOLESALEPR AS 'AVE.WHOLESALEPR'
  BY RATING
  WHERE CATEGORY EQ 'CLASSIC' OR 'FOREIGN' OR 'MUSICALS'
END

-* JOIN and FILTER interaction.
FILTER FILE EMPLOYEE CLEAR
  NAME=XXX WHERE JOBCODE EQ 'A01'
END
SET FILTER = XXX IN EMPLOYEE ON
TABLE FILE EMPLOYEE PRINT EMP_ID JOBCODE
END
JOIN JOBCODE IN EMPLOYEE TO JOBCODE IN JOBFILE
FILTER FILE EMPLOYEE
  NAME=XXX WHERE JOBCODE EQ 'A07'
END
TABLE FILE EMPLOYEE PRINT EMP_ID JOBCODE
END
SET FILTER = XXX IN EMPLOYEE ON
TABLE FILE EMPLOYEE PRINT EMP_ID JOBCODE
END
JOIN CLEAR *
TABLE FILE EMPLOYEE PRINT EMP_ID JOBCODE
END

-* Use positional field reference.
TABLE FILE CAR
  SUM AVE.DEALER_COST
  SUM AVE.DEALER_COST AND COMPUTE RATIO=C1/C2;
  BY COUNTRY
END

-* Calculate a simple moving average column.
DEFINE FILE GGSALES
  SDATE/YYM = DATE;
  SYEAR/Y = SDATE;
  SMONTH/M = SDATE;
  PERIOD/I2 = SMONTH;
END
TABLE FILE GGSALES
  SUM UNITS DOLLARS
  BY CATEGORY BY PERIOD
  WHERE SYEAR EQ 97 AND CATEGORY NE 'Gifts'
  ON PERIOD RECAP MOVAVE/D10.1=FORECAST(DOLLARS,1,3,'MOVAVE',3);
END

-* Define a function.
DEFINE FUNCTION SUBTRACT (VAL1/D8, VAL2/D8)
  SUBTRACT/D8.2 = VAL1 - VAL2;
END

-* Totals
TABLE FILE SALES
  SUM RETURNS DAMAGED AND ROW-TOTAL AND COLUMN-TOTAL
  BY PROD_CODE
END

-* Use data in heading and footing.
TABLE FILE EMPLOYEE
  HEADING
    "<DEPARTMENT>: BANK, EMPLOYEES AND SALARIES </1"
    "<15 COST OF VEHICLE IS <40 <RCOST>"
    "<10 <DIVISION <30 <AREA <50 <DATE"
    "DAILY REPORT <DATE <+5 <LOCATION <+5 <PRODUCT"
  PRINT CURR_SAL
  BY DEPARTMENT NOPRINT BY BANK_ACCT
  BY LAST_NAME BY FIRST_NAME
  ON DEPARTMENT PAGE-BREAK
  FOOTING
    "<DEPARTMENT EMPLOYEES WITH ELECTRONIC TRANSFER ACCOUNTS"
END

-* Long heading split up.
TABLE FILE EMPLOYEE
  HEADING
    "THIS HEADING <0X
SHOULD APPEAR <0X
ON ONE <0X
LINE"
  PRINT *
END

-* Real world example source code from
-* <http://www.informationbuilders.com/support/developers/dynamic_forms.html>.
-HTMLFORM CSTERM
-SET &ECHO = ALL;
-*
-* Replaces IDCAMS SYSIN
-*
DYNAM ALLOC FILE CATCTL NEW RECFM FB -
  LRECL 80 BLKSIZE 3120 SPACE 1,1 TRK REUSE
-*
-* Replaces IDCAMS SYSPRINT
-*
DYNAM ALLOC FILE CATLST NEW RECFM VBA -
  LRECL 125 BLKSIZE 27998 SPACE 2,1 TRK REUSE
-*
-* PRINT80 is a MFD used to read an 80 byte field named CONTENTS
-*
DYNAM ALLOC FILE PRNT80 NEW RECFM FB -
  LRECL 80 BLKSIZE 3120 SPACE 1,1 TRK REUSE
-*
-RUN
-*
-* Create the tape dataset name mask (first five nodes)
-*
-SET &CAT1 = ' LISTC LVL(' || '''' ||
-                         'PN0100.A143200.P87.P87800.STERM' || '''' || ') VOL';
-*
-WRITE CATCTL &CAT1
-*
-RUN
-*
-* Call the user written subroutine (i.e., GETCAT) to retrieve a list of tape datasets
-*
-SET &X = ' ';
-SET &X = GETCAT(&X,'A1');
-*
-* TABLE the catalog listing of tape datasets to a HOLD file
-*
DEFINE FILE PRNT80
  DSNAME/A44 = SUBSTR (80,CONTENTS,1,44,44,'A44');
END
-*
TABLE FILE PRNT80
  PRINT DSNAME
  ON TABLE HOLD
END
-*
DYNAM FREE FILE CATCTL
DYNAM FREE FILE CATLST
DYNAM FREE FILE PRNT80
-*
-RUN
-*
-*-----------------------------------------------------------------------
-*
-* Create the HTML drop down list
-*
DEFINE FILE HOLD
  NODE_POS/I3 = POSIT (DSNAME,44,'.SEM',4,'I3');
  WHATSEM/A4 = SUBSTR (44,DSNAME,NODE_POS+4,44,4,'A4');
  SEMWK/A2 = EDIT (WHATSEM,'$$99');
  SEMYR/A2 = EDIT (WHATSEM,'99$$');
  SEMESTERY2K/A6 = IF SEMYR LT '50' THEN ('20' | WHATSEM)
    ELSE ('19' | WHATSEM);
  D_SEM/A8 = DECODE SEMWK (03 'Spring'
    20 'Summer 1'
    26 'Summer 2'
    36 'Fall'
    ELSE 'ERROR');
  SEMTXT/A16 = D_SEM | ' - ' | WHATSEM;
-* Create the dynamic html for the drop down list
  SEMESTER/A50 = '<option value="' ||WHATSEM|| '">' ||SEMTXT|| '</option>';
END
-*
TABLE FILE HOLD
  SUM SEMESTER
-* Want the most recent semester first
  BY HIGHEST SEMESTERY2K NOPRINT
  ON TABLE SET HOLDLIST PRINTONLY
  ON TABLE HOLD AS HOLDSEMS
END
-*
-RUN
-*
-*-----------------------------------------------------------------------
-*
-SET &SPACE = '&' || 'nbsp;';
-*
-* Display the HTML form to allow users to select semester/cycle
-*
-HTMLFORM CSTERM
